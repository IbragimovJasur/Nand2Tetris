// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    // zx & nx
    Mux16(a=x, b=false, sel=zx, out=zxOut);
    Not16(in=zxOut, out=nxOut);
    Mux16(a=zxOut, b=nxOut, sel=nx, out=readyXOut);

    // zy & ny
    Mux16(a=y, b=false, sel=zy, out=zyOut);
    Not16(in=zyOut, out=nyOut);
    Mux16(a=zyOut, b=nyOut, sel=ny, out=readyYOut);

    // f
    And16(a=readyXOut, b=readyYOut, out=xyAnd);
    Add16(a=readyXOut, b=readyYOut, out=xyAdd);
    Mux16(a=xyAnd, b=xyAdd, sel=f, out=fxyOut);

    // ng & out
    Not16(in=fxyOut, out=nfxyOut);
    Mux16(a=fxyOut, b=nfxyOut, sel=no, out=out, out=orIn, out[15]=ng);

    // zr --> zero or positive output
    Or16Way(in=orIn, out=orOut);
    Not(in=orOut, out=zr);
}
