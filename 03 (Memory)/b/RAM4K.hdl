// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM4K.hdl
/**
 * Memory of 4K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM4K {
    // Info: can store 4096 - 2byte values == 8192 bytes (8kB)

    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    // Write
    DMux8Way(   // choosing banks of RAM512
        in=load, sel=address[9..11], 
        a=loadRAM512A, b=loadRAM512B, c=loadRAM512C, d=loadRAM512D, 
        e=loadRAM512E, f=loadRAM512F, g=loadRAM512G, h=loadRAM512H
    );

    // Banks of RAM512
    RAM512(in=in, load=loadRAM512A, address=address[0..8], out=outRAM512A);
    RAM512(in=in, load=loadRAM512B, address=address[0..8], out=outRAM512B);
    RAM512(in=in, load=loadRAM512C, address=address[0..8], out=outRAM512C);
    RAM512(in=in, load=loadRAM512D, address=address[0..8], out=outRAM512D);
    RAM512(in=in, load=loadRAM512E, address=address[0..8], out=outRAM512E);
    RAM512(in=in, load=loadRAM512F, address=address[0..8], out=outRAM512F);
    RAM512(in=in, load=loadRAM512G, address=address[0..8], out=outRAM512G);
    RAM512(in=in, load=loadRAM512H, address=address[0..8], out=outRAM512H);

    // Read
    Mux8Way16(
        a=outRAM512A, b=outRAM512B, c=outRAM512C, d=outRAM512D, 
        e=outRAM512E, f=outRAM512F, g=outRAM512G, h=outRAM512H, 
        sel=address[9..11], out=out
    );
}
